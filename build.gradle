plugins {
    id 'maven-publish'
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'java'

dependencies {
    // JAXP XML processing

    // DOM and SAX parsers:
    implementation group: 'xerces', name: 'xercesImpl', version: 'latest.release', {
        exclude group: 'xml-apis'
    }

    // XSLT transformer:
    implementation group: 'net.sf.saxon', name: 'Saxon-HE', version: 'latest.release'

    // RELAX NG validator:
    implementation group: 'isorelax', name: 'isorelax', version: 'latest.release'
    implementation group: 'com.thaiopensource', name: 'jing', version: 'latest.release', {
        exclude group: 'xerces'
        exclude group: 'xml-apis'
        exclude group: 'net.sf.saxon'
        exclude group: 'isorelax'
    }



    // JUnit 5 tests
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: 'latest.release'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: 'latest.release'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    // during tests, make sure nobody unexpectedly uses the system properties and
    // instantiates the XML classes internal to rt.jar (com.sun.org.apache...)
    systemProperties = [
        'jaxp.debug': '1',
        'javax.xml.parsers.SAXParserFactory': 'nu.mine.mosher.xml.SaxParserFactoryStub',
        'javax.xml.transform.TransformerFactory': 'nu.mine.mosher.xml.TransformerFactoryStub',
        'javax.xml.parsers.DocumentBuilderFactory': 'nu.mine.mosher.xml.DocumentBuilderFactoryStub'
    ]
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

group = 'nu.mine.mosher.xml'
sourceCompatibility = JavaVersion.VERSION_1_8
version = '1.0.0'
