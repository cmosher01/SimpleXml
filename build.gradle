buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:latest.release'
    }
}
apply plugin: 'java'
apply plugin: 'org.junit.platform.gradle.plugin'

group = 'nu.mine.mosher.xml'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // JAXP XML processing

    // DOM and SAX parsers;
    compile group: 'xerces', name: 'xercesImpl', version: 'latest.release', {
        exclude group: 'xml-apis'
    }

    // XSLT transformer:
    compile group: 'net.sf.saxon', name: 'Saxon-HE', version: 'latest.release'

    // RELAX NG validator:
    compile group: 'isorelax', name: 'isorelax', version: 'latest.release'
    compile group: 'com.thaiopensource', name: 'jing', version: 'latest.release', {
        exclude group: 'xerces'
        exclude group: 'xml-apis'
        exclude group: 'net.sf.saxon'
        exclude group: 'isorelax'
    }



    // JUnit 5 tests
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: 'latest.release'
    testCompileOnly group: 'org.apiguardian', name: 'apiguardian-api', version: 'latest.release'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: 'latest.release'
}

junitPlatformTest {
    // during tests, make sure nobody unexpectedly uses the system properties and
    // instantiates the XML classes internal to rt.jar (com.sun.org.apache...)
    systemProperties = [
            'jaxp.debug': '1',
            'javax.xml.parsers.SAXParserFactory': 'nu.mine.mosher.xml.SaxParserFactoryStub',
            'javax.xml.transform.TransformerFactory': 'nu.mine.mosher.xml.TransformerFactoryStub',
            'javax.xml.parsers.DocumentBuilderFactory': 'nu.mine.mosher.xml.DocumentBuilderFactoryStub'
    ]
}
